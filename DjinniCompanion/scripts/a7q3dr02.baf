// Sparring script of the right duelling duergar

IF
  Heard([ANYONE], HELP_ME_GROUP151)
  InMyGroup(LastHeardBy(Myself))
  Allegiance(LastHeardBy(Myself), ENEMY)
THEN RESPONSE #100
  ClearActions(Myself)
  Enemy()
END

IF
  Allegiance(Myself,ENEMY)
  Global("attacked","LOCALS",0)
THEN RESPONSE #100
  SetGlobal("attacked", "LOCALS", 1)
  DestroyItem("MINHP60")
  DestroyItem("A7Q3AX1")
  EquipMostDamagingMelee()
  Continue()
END

IF
  !Allegiance(Myself, ENEMY)
  OR(3)
    Detect([PC])
    Detect([FAMILIAR])
    Detect([CONTROLLED])
THEN
  RESPONSE #10
    DisplayStringNoNameHead(Myself, @3934)    // *Groan*
    Continue()
  RESPONSE #10
    DisplayStringNoNameHead(Myself, @3935)    // AARRGH!
    Continue()
  RESPONSE #10
    DisplayStringNoNameHead(Myself, @3936)    // HAAAAH!!
    Continue()
  RESPONSE #10
    DisplayStringNoNameHead(Myself, @3937)    // Come on!
    Continue()
  RESPONSE #10
    DisplayStringNoNameHead(Myself, @3933)    // I'm exhausted.
    Continue()
  RESPONSE #50
    Continue()
END

IF
  !Allegiance(Myself, ENEMY)
  See("A7Q3DR01")
  OR(3)
    Detect([PC])
    Detect([FAMILIAR])
    Detect([CONTROLLED])
THEN RESPONSE #100
  AttackOneRound("A7Q3DR01")
END

// *** Quest-specific actions for rakshasa leader Mahel in Quest 1 ***

// If party member is in range of the leader: cutscene -> party members are moved to fixed position, then dialog starts
IF
  Detect([PC])
  GlobalLT("A7Quest1", "GLOBAL", 2)
  !Allegiance(Myself, ENEMY)
THEN RESPONSE #100
  SetGlobal("A7FirstTalk", "MYAREA", 1)
  SetGlobal("A7Quest1", "GLOBAL", 2)
  ClearAllActions()
  StartCutSceneMode()
  FadeToColor([30.0], 0)
  Wait(2)
  ActionOverride(Player1, JumpToPoint([1208.1203]))
  ActionOverride(Player1, Face(8))
  ActionOverride(Player2, JumpToPoint([1168.1226]))
  ActionOverride(Player2, Face(8))
  ActionOverride(Player3, JumpToPoint([1250.1226]))
  ActionOverride(Player3, Face(8))
  ActionOverride(Player4, JumpToPoint([1208.1248]))
  ActionOverride(Player4, Face(8))
  ActionOverride(Player5, JumpToPoint([1154.1276]))
  ActionOverride(Player5, Face(8))
  ActionOverride(Player6, JumpToPoint([1258.1276]))
  ActionOverride(Player6, Face(8))
  MoveViewPoint([1208.1203], INSTANT)
  Wait(1)
  MultiPlayerSync()
  FadeFromColor([30.0], 0)
  Wait(2)
  StartDialogNoSet(Player1)
END

// If PC activates the stolen djinni lamp in sight of the rakshasa leader
IF
  Detect("A7Afaaq")
  Detect([PC])
  !Allegiance(Myself, ENEMY)
  Global("A7AfaaqMet", "GLOBAL", 1)
THEN RESPONSE #100
  StartDialogNoSet([PC])    // Force pickpocket dialog
END

// when attacked
IF
  AttackedBy([GOODCUTOFF], DEFAULT)
  !Allegiance(Myself, ENEMY)
THEN RESPONSE #100
  SetGlobal("A7RakAttacked", "A77001", 1)
  Enemy()
END

// turn hostile
IF
  Global("A7RakAttacked", "A77001", 1)
  !Allegiance(Myself, ENEMY)
THEN RESPONSE #100
  Enemy()
END

// teleport away
IF
  Global("A7RakTeleport", "A77001", 1)
  Global("RakTeleport", "LOCALS", 0)
THEN RESPONSE #100
  SetInterrupt(FALSE)
  SetGlobal("RakTeleport", "LOCALS", 1)
  ReallyForceSpell(Myself, DRYAD_TELEPORT)
  SetInterrupt(TRUE)
END

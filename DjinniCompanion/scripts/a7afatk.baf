// *** Battle actions for the djinni Afaaq in passive mode ***

// Reset help shout
IF
  OR(2)
    HPPercentGT(Myself, 50)
    Global("A7AfaaqGaseous", "GLOBAL", 1)
  Global("hurt33", "LOCALS", 1)
THEN RESPONSE #100
  SetGlobal("hurt33", "LOCALS", 0)
END

// When hurt then shout for help
IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See([EVILCUTOFF])
  HPPercentLT(Myself, 33)
  HPPercentGT(Myself, 19)
  Global("hurt33", "LOCALS", 0)
  Global("A7AfaaqGaseous", "GLOBAL", 0)
THEN RESPONSE #100
  SetGlobal("hurt33", "LOCALS", 1)
  DisplayStringHead(Myself, @60000)    // Ahh, no! Help me!
  Help()
END

// Improved Afaaq keeps himself hasted
IF
  Global("A7AfaaqUpgraded", "GLOBAL", 1)
  Global("AfaaqActiveHaste", "LOCALS", 1)
  !StateCheck(Myself, STATE_HASTED)
  CheckStat(Myself, 0, IMPROVEDHASTE)
  HaveSpellRES("A7SPIN3") // Haste
THEN
  RESPONSE #100
    SpellRES("A7SPIN3", Myself) // Haste
    Continue()
END

// *** Battle actions when trying to leave the party and CHARNAME refuses his request ***

// Afaaq loses the fight

// Situation: Afaaq leaves party after defeating Irenicus
IF
  HP(Myself, 1)
  Global("A7AfaaqLeaveParty", "GLOBAL", 3)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelLost", "LOCALS", 1)
  Continue()
END

IF
  InMyArea(Player1)
  OR(2)
    Global("AfaaqDuelLost", "LOCALS", 1)
    HP(Myself, 1)
  Range(Player1, 15)
  Global("A7AfaaqLeaveParty", "GLOBAL", 3)
THEN RESPONSE #100
  SetGlobal("A7AfaaqLeaveParty", "GLOBAL", 4)
  ClearAllActions()
  ApplySpellRES("A7REST", Myself)
  StartDialogueNoSet(Player1)
END

IF
  InMyArea(Player1)
  OR(2)
    Global("AfaaqDuelLost", "LOCALS", 1)
    HP(Myself, 1)
  !Range(Player1, 15)
  Global("A7AfaaqLeaveParty", "GLOBAL", 3)
THEN RESPONSE #100
  MoveToObject(Player1)
END


// Situation: Afaaq challenges party after CHARNAME betrayed the djinnis in Quest 5
IF
  HP(Myself, 1)
  Global("A7Q5ChallengePC", "GLOBAL", 2)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelLost", "LOCALS", 1)
  Continue()
END

IF
  InMyArea(Player1)
  OR(2)
    Global("AfaaqDuelLost", "LOCALS", 1)
    HP(Myself, 1)
  Range(Player1, 15)
  Global("A7Q5ChallengePC", "GLOBAL", 2)
THEN RESPONSE #100
  SetGlobal("A7Q5ChallengePC", "GLOBAL", 3)
  ClearAllActions()
  ApplySpellRES("A7REST", Myself)
  StartDialogueNoSet(Player1)
END

IF
  InMyArea(Player1)
  OR(2)
    Global("AfaaqDuelLost", "LOCALS", 1)
    HP(Myself, 1)
  !Range(Player1, 15)
  Global("A7Q5ChallengePC", "GLOBAL", 2)
THEN RESPONSE #100
  MoveToObject(Player1)
END



// Battle actions
// Use healing potions when wounded
IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  OR(2)
    See([PC])
    ActuallyInCombat()
  HasItem("POTN55", Myself)
  !GlobalTimerNotExpired("PotionUsedTimer", "LOCALS")
  HPPercentLT(Myself, 34)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobalTimer("PotionUsedTimer", "LOCALS", 6)
  DisplayString(Myself, @200)    // quaffs a potion
  UseItem("POTN55", Myself)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  OR(2)
    See([PC])
    ActuallyInCombat()
  HasItem("%itm_potn52%", Myself)
  !GlobalTimerNotExpired("PotionUsedTimer", "LOCALS")
  HPPercentLT(Myself, 50)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobalTimer("PotionUsedTimer", "LOCALS", 6)
  DisplayString(Myself, @200)    // quaffs a potion
  UseItem("%itm_potn52%", Myself)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  OR(2)
    See([PC])
    ActuallyInCombat()
  HasItem("POTN08", Myself)
  !GlobalTimerNotExpired("PotionUsedTimer", "LOCALS")
  HPPercentLT(Myself, 67)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobalTimer("PotionUsedTimer", "LOCALS", 6)
  DisplayString(Myself, @200)    // quaffs a potion
  UseItem("POTN08", Myself)
END


IF
  See([PC])
  !StateCheck(Myself, CD_STATE_NOTVALID)
  !StateCheck(Myself, STATE_INVISIBLE)
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep", "LOCALS", 1)
  SpellRES("A7SPIN1", Myself)        // Invisibility
END

IF
  See([PC])
  !StateCheck(Myself, CD_STATE_NOTVALID)
  StateCheck(Myself, STATE_INVISIBLE)
  HaveSpellRES("A7SPIN4")
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep", "LOCALS", 1)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep", "LOCALS", 2)
  SpellRES("A7SPIN4", Myself)        // Air Shield
END

IF
  See([PC])
  !StateCheck(Myself, CD_STATE_NOTVALID)
  StateCheck(Myself, STATE_INVISIBLE)
  !HasBounceEffects(Myself)
  !HasImmunityEffects(Myself) 
  HaveSpell(WIZARD_MINOR_SPELL_TURNING)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  Spell(Myself, WIZARD_MINOR_SPELL_TURNING)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See([PC])
  HaveSpell(WIZARD_MIRROR_IMAGE)
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep2", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep2", "LOCALS", 1)
  Spell(Myself, WIZARD_MIRROR_IMAGE)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  OR(2)
    StateCheck(Player1, STATE_INVISIBLE)
    StateCheck(Player1, STATE_IMPROVEDINVISIBILITY)
  Detect(Player1)
  !HaveSpell(WIZARD_TRUE_SIGHT)
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep3", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep3", "LOCALS", 1)
  ForceSpell(Myself, WIZARD_TRUE_SIGHT)
  Wait(4)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  OR(2)
    StateCheck(Player1, STATE_INVISIBLE)
    StateCheck(Player1, STATE_IMPROVEDINVISIBILITY)
  Detect(Player1)
  HaveSpell(WIZARD_TRUE_SIGHT)
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep3", "LOCALS", 0)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep3", "LOCALS", 1)
  Spell(Myself, WIZARD_TRUE_SIGHT)
END

IF
  See([PC])
  !StateCheck(Myself, CD_STATE_NOTVALID)
  !StateCheck(Myself, STATE_INVISIBLE)
  !StateCheck(Myself, STATE_IMPROVEDINVISIBILITY)
  HaveSpell(WIZARD_IMPROVED_INVISIBILITY)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  Spell(Myself, WIZARD_IMPROVED_INVISIBILITY)
END

IF
  See([PC])
  !StateCheck(Myself, CD_STATE_NOTVALID)
  !StateCheck(Myself, STATE_INVISIBLE)
  !StateCheck(Myself, STATE_IMPROVEDINVISIBILITY)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN 
  RESPONSE #50
    SpellRES("A7SPIN1", Myself)   // Invisibility
  RESPONSE #50
    Continue()
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See(Player1)
  HaveSpell(WIZARD_CHAIN_LIGHTNING)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  Spell(Player1, WIZARD_CHAIN_LIGHTNING)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See(Player1)
  HaveSpell(WIZARD_LIGHTNING_BOLT)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  Spell(Player1, WIZARD_LIGHTNING_BOLT)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See(Player1)
  !Range([PC], 4)
  HaveSpell(WIZARD_MAGIC_MISSILE)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  Spell(Player1, WIZARD_MAGIC_MISSILE)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See(Player1)
  Range([PC], 4)
  HaveSpell(WIZARD_MAGIC_MISSILE)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN 
  RESPONSE #50
    Spell(Player1, WIZARD_MAGIC_MISSILE)
  RESPONSE #50
    AttackReevaluate(LastSeenBy(Myself), 30)
END

IF
  !StateCheck(Myself, CD_STATE_NOTVALID)
  See([PC])
  Global("AfaaqDuelLost", "LOCALS", 0)
  Global("AfaaqDuelPrep", "LOCALS", 2)
THEN RESPONSE #100
  SetGlobal("AfaaqDuelPrep", "LOCALS", 3)
  SpellRES("A7SPIN3", Myself)        // Haste
END

IF
  AttackedBy([GOODCUTOFF], DEFAULT) 
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  AttackReevaluate(LastAttackerOf(Myself), 30)
END

IF
  !See([PC])
  !Range(Player1, 15)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  MoveToObject(Player1)
END

IF
  See(Player1)
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  AttackReevaluate(Player1, 30)
END

IF
  See([GOODCUTOFF])
  Global("AfaaqDuelLost", "LOCALS", 0)
THEN RESPONSE #100
  AttackReevaluate(LastSeenBy(Myself), 30)
END


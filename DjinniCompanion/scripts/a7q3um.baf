// Battle script for Tathas' Umber Hulks in Quest 3  

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  Global("firstUse","LOCALS",0)
THEN RESPONSE #100
  ForceSpell(LastSeenBy(Myself),HULK_CONFUSION)
  SetGlobal("firstUse","LOCALS",1)
  StartTimer("confus", 12)
END

IF
  TimerExpired("confus")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
THEN
  RESPONSE #25
    ForceSpell(LastSeenBy(Myself),HULK_CONFUSION)
    StartTimer("confus", 12)
  RESPONSE #25
    StartTimer("confus", 12)
  RESPONSE #25
    ForceSpell(LastSeenBy(Myself),HULK_CONFUSION)
    StartTimer("confus", 18)
  RESPONSE #25
    StartTimer("confus", 18)
END

IF
  See(NearestEnemyOf(Myself))
THEN RESPONSE #100
  AttackReevaluate(NearestEnemyOf(Myself),5)
END


// parts of WTASIGHT.BCS
IF
  Heard([ANYONE],WARNALARM)
THEN RESPONSE #100
  MoveToObject(NearestEnemyOf(Myself))
END

IF
  AttackedBy([GOODCUTOFF],DEFAULT)
  !Allegiance(Myself,ENEMY)
THEN RESPONSE #100
  Enemy()
END

IF
  See(NearestEnemyOf(Myself))
THEN RESPONSE #100
  EquipMostDamagingMelee()
  AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
  Allegiance(Myself,ENEMY)
  AttackedBy([ANYONE],DEFAULT)
THEN RESPONSE #100
  EquipMostDamagingMelee()
  Attack(LastAttackerOf(Myself))
END


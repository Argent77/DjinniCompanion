// *** Special combat script: Djinni (Quest 5) ***

// *** If no enemy is around ***
IF
  !See(NearestEnemyOf(Myself))
  AreaCheck("A77003")
  !Dead("A7Karim")
THEN 
  RESPONSE #50
    MoveToObject("A7Karim")
  RESPONSE #25
    MoveToObject(Player1)
  RESPONSE #25
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  AreaCheck("A77003")
  !Dead("A7Q5EF01")
THEN 
  RESPONSE #50
    MoveToObject(Player1)
  RESPONSE #25
    MoveToObject("A7Q5EF01")
  RESPONSE #25
    Continue()
END

IF
  !See(NearestEnemyOf(Myself))
  AreaCheck("A77003")
  InMyArea("A7Malik")
  !Dead("A7Malik")
THEN 
  RESPONSE #50
    MoveToObject(Player1)
  RESPONSE #25
    MoveToObject("A7Malik")
  RESPONSE #25
    Continue()
END


// *** Combat actions ***
IF
  See(NearestEnemyOf(Myself))
  Global("Prep", "LOCALS", 0)
THEN
  RESPONSE #50
    ApplySpell(Myself, WIZARD_PROTECTION_FROM_FIRE)
    SetGlobal("Prep", "LOCALS", 1)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  Global("SpellTrigger", "LOCALS", 0)
  !StateCheck(Myself, STATE_REALLY_DEAD)
THEN 
  RESPONSE #50
    DisplayString(Myself, 39968)  // Spell Trigger - Fired
    DisplayString(Myself, 2469)  // Spell Trigger - Protection from Magical Weapons
    ReallyForceSpell(Myself, WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
    DisplayString(Myself, 363)  // Spell Trigger - Minor Spell Turning
    ReallyForceSpell(Myself, WIZARD_MINOR_SPELL_TURNING)
    DisplayString(Myself, 40245)  // Spell Trigger - Globe of Invulnerability
    ReallyForceSpell(Myself, WIZARD_GLOBE_OF_INVULNERABILITY)
    SetGlobal("SpellTrigger", "LOCALS", 1)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  TookDamage()
  Global("Prep2", "LOCALS", 1)
  !StateCheck(Myself, STATE_REALLY_DEAD)
THEN RESPONSE #100
  SetGlobal("Prep2", "LOCALS", 2)
  DisplayString(Myself, 40252)  // Contingency - Protection from Magical Weapons
  ApplySpell(Myself, WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself, STATE_INVISIBLE)
  HaveSpellRES("A7SPIN1")
  Global("invis", "LOCALS", 0)
THEN 
  RESPONSE #25
    SetGlobal("invis", "LOCALS", 1)
    SpellRES("A7SPIN1", Myself)
  RESPONSE #75
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  TookDamage()
  HaveSpell(WIZARD_MIRROR_IMAGE)
THEN 
  RESPONSE #75
    Spell(Myself, WIZARD_MIRROR_IMAGE)
  RESPONSE #25
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  StateCheck(Myself, STATE_SILENCED)
THEN
  RESPONSE #50
    ForceSpell(Myself, WIZARD_VOCALIZE)
  RESPONSE #50
    Continue()
END

IF
  !HasBounceEffects(Myself)
  !HasImmunityEffects(Myself)
  HaveSpell(WIZARD_SPELL_TURNING)
  Allegiance(Myself, ENEMY)
THEN 
  RESPONSE #50
    Spell(Myself, WIZARD_SPELL_TURNING)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_DISPEL_MAGIC)
  Global("dispel", "LOCALS", 0)
THEN 
  RESPONSE #25
    SetGlobal("dispel", "LOCALS", 1)
    Spell(LastSeenBy(Myself), WIZARD_DISPEL_MAGIC)
  RESPONSE #75
    Continue()
END

IF
  !CheckStat(NearestEnemyOfType([0.0.0.MAGE_ALL]), 0, STONESKINS)
  See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
  HaveSpell(WIZARD_BREACH)
THEN RESPONSE #100
  Spell(LastSeenBy(Myself), WIZARD_BREACH)
END

IF
  !CheckStat(NearestEnemyOfType([0.0.0.DRUID_ALL]), 0, STONESKINS)
  See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
  HaveSpell(WIZARD_BREACH)
THEN RESPONSE #100
  Spell(LastSeenBy(Myself), WIZARD_BREACH)
END

IF
  !CheckStat(NearestEnemyOfType([0.0.0.BARD_ALL]), 0, STONESKINS)
  See(NearestEnemyOfType([0.0.0.BARD_ALL]))
  HaveSpell(WIZARD_BREACH)
THEN RESPONSE #100
  Spell(LastSeenBy(Myself), WIZARD_BREACH)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_CHAIN_LIGHTNING)
THEN 
  RESPONSE #25
    Spell(LastSeenBy(Myself), WIZARD_CHAIN_LIGHTNING)
  RESPONSE #75
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_DISPEL_MAGIC)
  Global("dispel", "LOCALS", 1)
THEN 
  RESPONSE #50
    SetGlobal("dispel", "LOCALS", 2)
    Spell(LastSeenBy(Myself), WIZARD_DISPEL_MAGIC)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MELF_ACID_ARROW)
THEN 
  RESPONSE #25
    Spell(LastSeenBy(Myself), WIZARD_MELF_ACID_ARROW)
  RESPONSE #75
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MAGIC_MISSILE)
THEN 
  RESPONSE #50
    Spell(LastSeenBy(Myself), WIZARD_MAGIC_MISSILE)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpellRES("A7SPIN2")
THEN 
  RESPONSE #50
    SpellRES("A7SPIN2", Myself)
  RESPONSE #50
    Continue()
END

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself, STATE_INVISIBLE)
  HaveSpellRES("A7SPIN1")
  GlobalLT("invis", "LOCALS", 5)
THEN 
  RESPONSE #25
    IncrementGlobal("invis", "LOCALS", 1)
    SpellRES("A7SPIN1", Myself)
  RESPONSE #75
    Continue()
END

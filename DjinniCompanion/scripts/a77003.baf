// *** Area script for "Magma Cavern" ***

// If quest has been cancelled
IF
  Global("A7Quest5", "GLOBAL", 11)
  Global("A7Q5Cancelled", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5Cancelled", "A77003", 1)
  FadeToColor([0.0], 0)
  Wait(1)
  FadeFromColor([45.0], 0)
  Continue()
END

// adjusting specifics of the salamanders when joining the efreets
IF
  Global("A7Q5SalamanderAllied", "A77003", 1)
  Global("A7Q5SalamanderSpecifics", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5SalamanderSpecifics", "A77003", 1)
  ChangeSpecifics("A7Q5FS01", %specific_efreeti%)
  ChangeSpecifics("A7Q5FS02", %specific_efreeti%)
  ChangeSpecifics("A7Q5FS03", %specific_efreeti%)
  ChangeSpecifics("A7Q5FS04", %specific_efreeti%)
  ChangeSpecifics("A7Q5FS05", %specific_efreeti%)
  ChangeSpecifics("A7Q5FS06", %specific_efreeti%)
  ChangeSpecifics("A7Q5FES", %specific_efreeti%)
END

// advance quest stage when the efreets turn hostile
IF
  Global("A7Q5EfreetiAttacked", "GLOBAL", 1)
  GlobalLT("A7Quest5", "GLOBAL", 8)
  Global("A7Q5MalikEntered", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Quest5", "GLOBAL", 8)
END

// advance quest stage when the efreets turn hostile and Malik is around
IF
  Global("A7Q5EfreetiAttacked", "GLOBAL", 1)
  GlobalLT("A7Quest5", "GLOBAL", 8)
  Global("A7Q5MalikEntered", "A77003", 1)
THEN RESPONSE #100
  SetGlobal("A7Quest5", "GLOBAL", 9)
END

// Malik enters the cave as enemy
IF
  Global("A7Quest5", "GLOBAL", 8)
  OR(7)
    Dead("A7Q5EF01")
    Dead("A7Q5EF02")
    Dead("A7Q5EF03")
    Dead("A7Q5EF04")
    Dead("A7Q5EF05")
    Dead("A7Q5EF06")
    Dead("A7Q5EF07")
  Global("A7Q5MalikEntered", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5MalikEntered", "A77003", 1)
  SetGlobal("A7Quest5", "GLOBAL", 9)
  ScreenShake([5.5], 20)
  SmallWait(5)
  CreateVisualEffect("A703FIR1", [2433.1578])
  Wait(1)
  CreateCreature("A7Malik", [2433.1578], 2)
END

// *** Mark the djinn help subquest as completed when all djinns are dead ***
IF
  Global("A7Q5DjinniSummoned", "GLOBAL", 1)
  Dead("A7Karim")
  Dead("A7Q5DJ01")
  Dead("A7Q5DJ02")
  Dead("A7Q5DJ03")
  Dead("A7Q5DJ04")
  Dead("A7Q5DJ05")
THEN RESPONSE #100
  SetGlobal("A7Q5DjinniSummoned", "GLOBAL", 2)
END

// *** Advance quest after defeating the efreets ***
IF
  GlobalLT("A7Quest5", "GLOBAL", 10)
//  Global("A7Q5EfreetiAttacked", "GLOBAL", 1)
  Dead("A7Malik")
  Dead("A7Q5EF01")
  Dead("A7Q5EF02")
  Dead("A7Q5EF03")
  Dead("A7Q5EF04")
  Dead("A7Q5EF05")
  Dead("A7Q5EF06")
  Dead("A7Q5EF07")
  Dead("A7Q5EF08")
  !ActuallyInCombat()
THEN RESPONSE #100
  SetGlobal("A7Quest5", "GLOBAL", 10)
END


// After the efreets are gone: salamaders take over
IF
  Global("A7Quest5", "GLOBAL", 10)
  Global("A7Q5SalamanderAttacked", "GLOBAL", 0)
  Global("A7Q5SalamandersTakeOver", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5SalamandersTakeOver", "A77003", 1)
END


// spawn a group of mephits when party needs vials of mephit blood
IF
  Global("A7Q5SubQuest1", "A77003", 3)
  Global("A7Q5MephitSpawned", "A77003", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5MephitSpawned", "A77003", 1)
  CreateCreature("A7Q5fm", [502.2572], 10)
  CreateCreature("A7Q5mm", [476.2475], 8)
  CreateCreature("A7Q5sm", [612.2562], 12)
END


// Warn about expiring heat protection (as dialog line)
IF
  !StateCheck(Player1, CD_STATE_NOTVALID)
  !ActuallyInCombat()
  !GlobalTimerNotExpired("A7Q5ProtectionWarnTimer", "GLOBAL")
  GlobalTimerNotExpired("A7Q5ProtectionTimer", "GLOBAL")
  Global("A7Q5Protection", "GLOBAL", 1)
  Global("A7Q5ProtectionWarn", "GLOBAL", 0)
THEN RESPONSE #100
  SetGlobal("A7Q5ProtectionWarn", "GLOBAL", 1)
  ActionOverride(Player1, StartDialogOverride("Player1", Player1))
END

// Warn about expiring heat protection (as display string)
IF
  !StateCheck(Player1, CD_STATE_NOTVALID)
  ActuallyInCombat()
  Global("A7Q5Protection", "GLOBAL", 1)
  Global("A7Q5ProtectionWarn", "GLOBAL", 0)
  !GlobalTimerNotExpired("A7Q5ProtectionWarnTimer", "GLOBAL")
  GlobalTimerNotExpired("A7Q5ProtectionTimer", "GLOBAL")
THEN RESPONSE #100
  SetGlobal("A7Q5ProtectionWarn", "GLOBAL", 1)
  DisplayStringNoNameHead(Player1, @6815)    // It is already over six hours ago you have applied protection from heat on your party.
END

// Heat protection ends
IF
  Global("A7Q5Protection", "GLOBAL", 1)
  !GlobalTimerNotExpired("A7Q5ProtectionWarnTimer", "GLOBAL")
  !GlobalTimerNotExpired("A7Q5ProtectionTimer", "GLOBAL")
THEN RESPONSE #100
  SetGlobal("A7Q5Protection", "GLOBAL", 0)
  SetGlobal("A7Q5ProtectionWarn", "GLOBAL", 1)
  DisplayStringNoNameHead(Player1, @6816)    // Protection from Heat has run out.
END


// Apply fire damage when not protected from infernal heat or not immune to fire
IF
  Delay(3)
  CheckStatLT(Player1, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player1, TRAP_MUCK)
  Continue()
END

IF
  Delay(3)
  CheckStatLT(Player2, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player2, TRAP_MUCK)
  Continue()
END

IF
  Delay(3)
  CheckStatLT(Player3, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player3, TRAP_MUCK)
  Continue()
END

IF
  Delay(3)
  CheckStatLT(Player4, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player4, TRAP_MUCK)
  Continue()
END

IF
  Delay(3)
  CheckStatLT(Player5, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player5, TRAP_MUCK)
  Continue()
END

IF
  Delay(3)
  CheckStatLT(Player6, 100, RESISTFIRE)
  Global("A7Q5Protection", "GLOBAL", 0)
THEN RESPONSE #100
  ApplySpell(Player6, TRAP_MUCK)
  Continue()
END

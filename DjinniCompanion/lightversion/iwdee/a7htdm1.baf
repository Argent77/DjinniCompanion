// *** Modified Succubus combat script from BG2EE ***

IF
  See([0.0.0.IMP.0.0.LAWFUL_EVIL])
THEN
  RESPONSE #100
    AttackReevaluate([0.0.0.IMP.0.0.LAWFUL_EVIL],30)
END

IF
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,50)
  !StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerNotExpired("Ethereal","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Ethereal","LOCALS",6)
    ReallyForceSpellRES("A7SUC01",Myself) // Ethereal
END

IF
  HPPercentLT(Myself,50)
  StateCheck(Myself,STATE_INVISIBLE)
  Range(NearestEnemyOf(Myself),20)
THEN
  RESPONSE #100
    SetGlobal("running","LOCALS",1)
    RunAwayFrom(NearestEnemyOf(Myself),30)
END

IF
  HPPercentLT(Myself,50)
  Global("running","LOCALS",1)
  !Range(NearestEnemyOf(Myself),20)
THEN
  RESPONSE #100
    ForceSpell(Myself,CLERIC_HEAL) // SPPR607.SPL (Heal)
END

IF
  OR(3)
    See([GOODCUTOFF.0.0.BARD_ALL])
    See([GOODCUTOFF.0.0.LONG_BOW])
    See([GOODCUTOFF.0.0.SORCERER])
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("prep","LOCALS")
  RandomNum(2,1)
THEN
  RESPONSE #100
    SetGlobalTimer("prep","LOCALS",1000)
    SpellNoDec(Myself,WIZARD_MINOR_SPELL_TURNING) // SPWI522.SPL (Minor Spell Turning)
END

IF
  See(NearestEnemyOf(Myself))
  See([GOODCUTOFF.0.0.0.0.MALE])
  !Difficulty(EASIEST)
  Range(LastSeenBy(Myself),30)
  RandomNum(2,1)
  !StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  !StateCheck(LastSeenBy(Myself),STATE_PANIC)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !GlobalTimerNotExpired("Charm","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Charm","LOCALS",6)
    SpellNoDecRES("a7suc02",[GOODCUTOFF.0.0.0.0.MALE]) // Charm Person
END

IF
  See(NearestMyGroupOfType([EVILCUTOFF.0.0.TANARI]))
  HPPercentLT(LastSeenBy(Myself),50)
  !HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("Healing","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Healing","LOCALS",6)
    SetInterrupt(FALSE)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CURE_CRITICAL_WOUNDS) // SPPR502.SPL (Cure Critical Wounds)
    SetInterrupt(TRUE)
END

IF
  See(SecondNearestMyGroupOfType([EVILCUTOFF.0.0.TANARI]))
  HPPercentLT(LastSeenBy(Myself),50)
  !HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("Healing","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Healing","LOCALS",6)
    SetInterrupt(FALSE)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CURE_CRITICAL_WOUNDS) // SPPR502.SPL (Cure Critical Wounds)
    SetInterrupt(TRUE)
END

IF
  See(ThirdNearestMyGroupOfType([EVILCUTOFF.0.0.TANARI]))
  HPPercentLT(LastSeenBy(Myself),50)
  !HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("Healing","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("Healing","LOCALS",6)
    SetInterrupt(FALSE)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CURE_CRITICAL_WOUNDS) // SPPR502.SPL (Cure Critical Wounds)
    SetInterrupt(TRUE)
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),30)
END
